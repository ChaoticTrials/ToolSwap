plugins {
    id 'org.moddingx.modgradle.meta' version '5.0.2'
}

repositories {
    maven {
        name = 'ModMaven'
        url = 'https://modmaven.dev/'
    }
}

var dev = System.getenv('IS_DEV')?.toBoolean() ?: false
var baseVersion = '21.0'
var modBaseVersion = '1.21-6.0'

mod.configure {
    modid 'toolswap'
    group 'de.melanx'
    versioning {
        if (dev) {
            def gitCommitHash = ['git', 'rev-parse', 'HEAD'].execute().text.trim()
            constant baseVersion + (gitCommitHash ? "-${gitCommitHash}" : '')
        } else {
            base modBaseVersion
            maven 'https://maven.melanx.de/release'
        }
    }

    neoforge project.hasProperty('neo_version') ? project.neo_version : "${baseVersion}.167"

    license 'The Apache License, Version 2.0'
    github 'ChaoticTrials/ToolSwap'

    artifacts {
        sources {
            publishToRepositories()
        }
    }

    changelog {
        def lines = defaultChangelog.toString().split('\n')
        def filteredChangelog = lines.findAll { !it.startsWith('- [meta]') }.join('\n')

        return filteredChangelog
    }


    publishing {
        maven {
            name 'melanx'
            url 'https://maven.melanx.de/release'
            credentials(PasswordCredentials)
        }
    }

    upload {
        all {
            type 'release'
            version '1.21'
            version '1.21.1'
            version '1.21.2'
            version '1.21.3'
            version '1.21.4'
        }
        curseforge {
            projectId 361977
        }
        modrinth {
            projectId 'L9JLNLqk'
        }
    }
}
